#+ summary: Get genes given an interval.
#+ tags:
#+  - Get genes given an interval.
#+ enumerate:
#+  - chro:
#+    - http://rdf.ebi.ac.uk/resource/ensembl/33/arabidopsis_thaliana/TAIR10/1
#+    - http://rdf.ebi.ac.uk/resource/ensembl/33/arabidopsis_thaliana/TAIR10/2
#+    - http://rdf.ebi.ac.uk/resource/ensembl/33/arabidopsis_thaliana/TAIR10/3
#+    - http://rdf.ebi.ac.uk/resource/ensembl/33/arabidopsis_thaliana/TAIR10/4
#+    - http://rdf.ebi.ac.uk/resource/ensembl/33/arabidopsis_thaliana/TAIR10/5

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX faldo: <http://biohackathon.org/resource/faldo#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX bif: <bif:>
prefix TAIR10: <http://rdf.ebi.ac.uk/resource/ensembl/33/arabidopsis_thaliana/TAIR10/>

SELECT
  *
WHERE {
    GRAPH <http://plants.ensembl.org/arabidopsis_thaliana> {
    SELECT
      ?gene_label
      ?feature_name
      ?begin_pos
      ?end_pos
    WHERE {
    GRAPH <http://purl.obolibrary.org/obo/so.owl> {
      ?feature_type a owl:Class ;
         rdfs:label ?feature_name .
      ?feature_name bif:contains "'protein_coding_gene'" .
    }
    ?feature a ?feature_type ;
      rdfs:label ?gene_label ;
      faldo:location ?loc .
    ?loc faldo:begin ?begin ;
      faldo:end ?end .
    ?begin faldo:position ?begin_pos ;
      faldo:reference TAIR10:1 .
    ?end faldo:position ?end_pos ;
      faldo:reference TAIR10:1 .
    }
  }
  FILTER(?begin_pos >= 0 &&
         ?end_pos <= 200000)
}